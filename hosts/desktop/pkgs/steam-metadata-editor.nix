# Config/steam-metadata-editor.nix
{ lib
, stdenv
, fetchFromGitHub
, python3
, makeDesktopItem
, copyDesktopItems
}:

let
  python = python3.withPackages (ps: with ps; [
    tkinter
  ]);
in
stdenv.mkDerivation rec {
  pname = "steam-metadata-editor";
  version = "unstable-2025-03-12";

  src = fetchFromGitHub {
    owner = "tralph3";
    repo = "Steam-Metadata-Editor";
    rev = "5c6ec345417c48160ea9798d97643c6f0e82ba7d";
    hash = "sha256-+80NYqzTjWA7JZxHx0N1R96/B/XKtvnILhJ06JMwcX4=";
  };

  nativeBuildInputs = [ copyDesktopItems ];
  buildInputs = [ python ];

  dontBuild = true;

  installPhase = ''
    runHook preInstall

    # Copier les sources Python
    mkdir -p $out/lib/steam-metadata-editor
    cp -r src/* $out/lib/steam-metadata-editor/

    # Wrapper qui lance directement main.py
    mkdir -p $out/bin
    cat > $out/bin/steam-metadata-editor << EOF
#!/usr/bin/env bash
exec ${python}/bin/python3 $out/lib/steam-metadata-editor/main.py "\$@"
EOF
    chmod +x $out/bin/steam-metadata-editor

    # Icône
    install -Dm644 steam-metadata-editor.png \
      $out/share/icons/hicolor/256x256/apps/steam-metadata-editor.png

    runHook postInstall
  '';

  desktopItems = [
    (makeDesktopItem {
      name = "steam-metadata-editor";
      exec = "steam-metadata-editor";
      icon = "steam-metadata-editor";
      desktopName = "Steam Metadata Editor";
      comment = "Edit metadata of your Steam apps";
      categories = [ "Utility" "Game" ];
    })
  ];

  meta = with lib; {
    description = "GUI editor for Steam app metadata (titles, launch menus, etc.)";
    homepage = "https://github.com/tralph3/Steam-Metadata-Editor";
    license = licenses.gpl3Only;
    platforms = platforms.linux;
    mainProgram = "steam-metadata-editor";
  };
}
